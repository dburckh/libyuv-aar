plugins {
    id 'com.android.library'
    id 'maven-publish'
}

android {
    namespace 'com.homesoft.libyuv'
    compileSdk 33

    defaultConfig {
        minSdk 21
        targetSdk 33

        externalNativeBuild {
            cmake {
                cppFlags ""
                //arguments "-DANDROID_STL=c++_shared"
                arguments "-DANDROID_STL=none"
            }
        }
    }
    buildFeatures {
        prefabPublishing true
    }
    prefab {
        yuv {
            headers "src/main/cpp/libyuv/include"
        }
    }
    buildTypes {
        release {
            minifyEnabled false
        }
    }
    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
//            version "3.22.1"
        }
    }
//    ndkVersion '25.1.8937393'
}
//Install to local maven repo
//gradlew yuv:publishToMavenLocal

//gradlew yuv:publish
publishing {
    publications {
        yuv(MavenPublication) {
            groupId = "${rootProject.ext.groupId}"
            artifactId = "${rootProject.ext.artifactId}"
            version "${rootProject.ext.artifactVersion}"
            artifact("$buildDir/outputs/aar/yuv-release.aar")
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            /** Configure path of your package repository on Github
             ** Replace GITHUB_USERID with your/organisation Github userID
             ** and REPOSITORY with the repository name on GitHub
             */
            url = uri("https://maven.pkg.github.com/dburckh/libyuv-aar")
            credentials {
                /** Create github.properties in root project folder file with
                 ** gpr.usr=GITHUB_USER_ID & gpr.key=PERSONAL_ACCESS_TOKEN
                 ** Set env variable GPR_USER & GPR_API_KEY if not adding a properties file**/

                username = System.getenv("GPR_USER")
                password = System.getenv("GPR_API_KEY")
            }
        }
    }
}
dependencies {
}