plugins {
    id 'com.android.library'
    id 'maven-publish'
}

android {
    namespace 'com.homesoft.yuv'
    compileSdk 34

    // This is the default, but Jitpack.io requires it to be explicit
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    defaultConfig {
        minSdk 21
        targetSdk 34

        externalNativeBuild {
            cmake {
                //cppFlags ""
                //arguments "-DANDROID_STL=c++_shared"
                arguments "-DANDROID_STL=none"
            }
        }
    }
    buildFeatures {
        prefabPublishing true
    }
    prefab {
        yuv {
            headers "src/main/cpp/libyuv/include"
        }
    }
    buildTypes {
        release {
            minifyEnabled false
        }
    }
    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
//            version "3.22.1"
        }
    }
    publishing {
        singleVariant("release")
    }
//    ndkVersion '25.1.8937393'
}
//Install to local maven repo
//gradlew yuv:publishToMavenLocal
publishing {
    publications {
        release(MavenPublication) {
            // Jitpack overrides these,
            // but they are necessary for local testing
            groupId = 'com.github.dburckh'
            artifactId = 'libyuv-aar'
            version = '0.9.1'

            afterEvaluate {
                from components.release
            }
        }
    }
}
dependencies {
}